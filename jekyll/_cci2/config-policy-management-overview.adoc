---
description: Config policies for CircleCI project configurations.
contentTags:
  platform:
  - Cloud
---
= Config policies overview - Open preview
:page-layout: classic-docs
:page-liquid:
:icons: font
:experimental:

NOTE: The config policies feature is available on the **Scale** plan and is currently in **open preview**.

Use config policies to create organization-level policies to impose rules and scopes to govern which configuration elements are required, allowed, not allowed etc.

[#introduction]
== Introduction

Config policies allow you to create policies for governing CircleCI project configurations. In its strictest case, if a project configuration does not comply with the rules set out in the associated policy, that project's pipelines cannot be triggered until it does comply.

CircleCI uses `config.yml` files to define CI/CD pipelines at the project level. This is a convenient way of developing and iterating quickly, as each pipeline can be made to meet the needs of the project as it grows. However, it can be difficult to manage and enforce organization-wide conventions and security policies. Config policies adds in this layer of control.

Config policy decisions are stored, and can be audited. This provides useful data about the pipeline definitions being run within your organization.

[#quickstart]
== Quickstart

If you already have a xref:first-steps[CircleCI account] connected to your VCS, and you would like to get started with config policies right away, check out the steps in the xref:create-and-manage-config-policies#create-a-policy[Create a policy] how-to guide to get started.

The following how-to guides are available for config policies:

* xref:create-and-manage-config-policies#[Create and manage config policies]
* xref:test-config-policies#[Test config policies]
* xref:use-the-cli-for-config-and-policy-development#[Use the CLI for config and policy development]
* xref:config-policies-for-self-hosted-runner#[Config policies for self-hosted runner]

[#how-config-policy-work]
== How config policies work

Config policies use a decision engine based on link:https://www.openpolicyagent.org/[Open Policy Agent (OPA)]. Policies are written in the Rego query language, as defined by OPA. Rule violations are surfaced when pipeline triggers are applied.

Policies can be developed locally and pushed to CircleCI using the CircleCI CLI. Policies can be saved to a repository within your VCS. For more information see the link:/docs/create-and-manage-config-policies[Create and manage config policies] page.

CircleCI uses the result of OPA output to generate _decisions_ from policy executions. Those decisions have the following outputs:

```
status:           PASS | SOFT_FAIL | HARD_FAIL | ERROR
reason:           string                                      (optional, only present when status is ERROR)
enabled_rules:    Array<string>
soft_failures:    Array<{ rule: string, reason: string }>
hard_failures:    Array<{ rule: string, reason: string }>
```

The decision result tracks which rules were included in the making of the decision, and any violations, both soft and hard, that were found.

[#use-the-cli-with-config-policies]
== Use the CLI with config policies

Use the CircleCI CLI to manage your organization's policies programmatically. Config policies sub-commands are grouped under the `circleci policy` command.

The following config policies sub-commands are currently supported within the CLI:

* `diff` - shows the difference between local and remote policy bundles
* `fetch` - fetches the policy bundle (or one policy, based on name) from remote
* `push` - pushes the policy bundle (activate policy bundle)
+
For example, running the following command activates a policy bundle stored at `./policy_bundle_dir_path`:
+
[source,shell]
----
circleci policy push ./policy_bundle_dir_path --owner-id <your-organization-ID>
----
*  `settings` - used to modify config policies settings as required.
+
When the `settings` sub-command is called without any flags, current settings are fetched and printed on console.
+
[source,shell]
----
circleci policy settings --owner-id <your-organization-ID>
----
+
Example output:
+
[source,shell]
----
{
  "enabled": false
}
----
* `test` - used to run tests on your policies.
+
For example, running the following command runs all defined tests in the `policies` directory:
+
[source,shell]
----
circleci policy test ./policies
----
+
Example output:
+
[source,shell]
----
ok    policies    0.001s

3/3 tests passed (0.001s)
----

{% include snippets/find-organization-id.adoc %}

[#writing-rego-policies-using-circleci-domain-specific-language]
== Writing Rego policies using CircleCI domain-specific language

Policies are written in Rego, a purpose-built declarative policy language that supports OPA. You can find more about Rego in the link:https://www.openpolicyagent.org/docs/latest/policy-language/[`rego` language docs].

In order for CircleCI to make decisions about your configs, it needs to be able to interpret the output
generated when your policies are evaluated. Therefore, policies must be written to meet the CircleCI specifications detailed on this page.

[#package-and-name]
=== Package and name

All policies must belong to the `org` package and declare the policy name as the first rule. All policy Rego files should include:

[source,rego]
----
package org

policy_name["unique_policy_name"]
----

The `policy_name` is an alphanumeric string with a maximum length of 80 characters. It is used to uniquely identify the policy by name within the system (similar to a Kubernetes named resource).
Policy names must be unique. Two policies cannot have the same name within an organization.

The `policy_name` must be declared using a partial rule and declare the name as a `rego key`: `policy_name["NAME"]`.

[#rules]
=== Rules

After declaring the `org` package and `policy_name` rule, policies can then be defined as a list of rules. Each rule is composed of three parts:

* Evaluation - Evaluates whether the config contains the policy violation.
* Enforcement status - Determines how a violation should be enforced.
* Enablement - Determines if a policy violation should be enabled.

Using this format allows policy writers to create custom helper functions without impacting CircleCI's ability to parse policy evaluation output. You can create your own helper functions, but also CircleCI provides a set of helpers by importing `data.circleci.config` in your policies. For more information, see the link:/docs/config-policy-reference[Config policy reference].

NOTE: **Helpers** in the context of config policies are rules like any other, but rules that are not individually _enabled_ for the process of determining policy violation. Helpers can be written and used as building blocks for your policies.

Policies all have access to config data through the `input` variable. The `input` is the project config being evaluated. Since the `input` matches the CircleCI config, you can write rules to enforce a desired state on any available config element, for example, `jobs` or `workflows`.

[source,rego]
----
input.workflows     # an array of nested structures mirroring workflows in the CircleCI config
input.jobs          # an array of nested structures mirroring jobs in the CircleCI config
----

[#define-a-rule]
==== Define a rule

In OPA, rules can produce any type of output. At CircleCI, rules that produce violations must have outputs of the following types:

* String
* String array
* Map of string to string

This is because rule violations must produce error messages that individual contributors and SecOps can act upon.
Helper rules that produce differently typed outputs can still be defined, but rules that will be considered when making CircleCI decisions must have the output types specified above. For more information see the <<#enablement>> section below.

[#evaluation]
===== Evaluation

This is how the decision engine determines if a config violates the given policy. The evaluation defines the name and ID of the rule, checks a condition, and returns a user-friendly string describing the violation. Rule evaluations include the **rule name** and an **optional rule ID**. The rule name will be used to enable and set the enforcement level for a rule.

[source,rego]
----
RULE_NAME = reason {
  ... # some comparison
  reason := "..."
}
----

[source,rego]
----
RULE_NAME[RULE_ID] = reason {
  ... # some comparison
  reason := "..."
}
----

Here is an example of a simple evaluation that checks that a config includes at least one workflow:

[source,rego]
----
contains_workflows = reason {
    count(input.workflows) > 0
    reason := "config must contain at least one workflow"
}
----

The rule ID can be used to differentiate between multiple violations of the same rule. For example, if a config uses multiple unofficial Docker images, this might lead to multiple violations of a `use_official_docker_image` rule. Rule IDs should only be used when multiple violations are expected. In some cases, the customer may only need to know if a rule passes or not. In this case, the rule will not need a rule ID.

[source,rego]
----
use_official_docker_image[image] = reason {
  some image in docker_images   # docker_images are parsed below
  not startswith(image, "circleci")
  not startswith(image, "cimg")
  reason := sprintf("%s is not an approved Docker image", [image])
}

# helper to parse docker images from the config
docker_images := {image | walk(input, [path, value])  # walk the entire config tree
                          path[_] == "docker"         # find any settings that match 'docker'
                          image := value[_].image}    # grab the images from that section

----

[#enforcement]
===== Enforcement

The policy service allows rules to be enforced at different levels.

[source,rego]
----
ENFORCEMENT_STATUS["RULE_NAME"]
----

The two available enforcement levels are:

* `hard_fail` - If the `policy-service` detects that the config violated a rule set as `hard_fail`, the pipeline will not be triggered.
* `soft_fail` - If the `policy-service` detects that the config violated a rule set as `soft_fail`, the pipeline will be triggered and the violation will be logged in the `policy-service` decision log.

An example of setting the `use_official_docker_image` rule to `hard_fail`:

[source,rego]
----
hard_fail["use_official_docker_image"]
----

[#enablement]
===== Enablement

A rule must be enabled for it to be inspected for policy violations. Rules that are not enabled do not need to match CircleCI violation output formats, and can be used as helpers for other rules.

[source,rego]
----
enable_rule["RULE_NAME"]
----

To enable a rule, add the rule as a key in the `enable_rule` object. For example, to enable the rule `use_official_docker_image`, use the following:

[source,rego]
----
enable_rule["use_official_docker_image"]
----

Use `enable_hard` to enable a rule and set its enforcement level to `hard` in a single statement.

The following statements are equivalent:
[source,rego]
----
enable_hard["use_official_docker_image"]
----

[source,rego]
----
enable_rule["use_official_docker_image"]

hard_fail["use_official_docker_image"]
----

[#using-pipeline-metadata]
=== Using pipeline metadata

When writing policies for circleci config, it is often desirable to have policies that vary slightly in behaviour by project or branch. This is possible using the `data.meta` Rego  property.

When a policy is evaluated in the context of a triggered pipeline the following properties will be available on `data.meta`:

* `project_id` (CircleCI Project UUID)
* `build_number` (number)
* `ssh_rerun` (boolean) - indicates if CI job is started using the SSH rerun feature

* `vcs.branch` (string)
* `vcs.release_tag` (string)
* `vcs.origin_repository_url` (string) - URL to the repository where the commit was made (this will only be different in the case of a forked pull request)
* `vcs.target_repository_url` (string) - URL to the repository building the commit

This metadata can be used to activate/deactivate rules, modify enforcement statuses, and be part of the rule definitions themselves.

The following is an example of a policy that only runs its rule for a single project and enforces it as `hard_fail` only on branch main.

[source,rego]
----
package org

policy_name["example"]

# specific project UUID
# use care to avoid naming collisions as assignments are global across the entire policy bundle
sample_project_id := "c2af7012-076a-11ed-84e6-f7fa45ad0fd1"

# this rule is enabled only if the body evaluates to true
enable_rule["custom_rule"] { data.meta.project_id == sample_project_id }

# "custom_rule" evaluates to a hard_failure condition only if run in the context of branch main
hard_fail["custom_rule"] { data.meta.vcs.branch == "main" }
----

The following is an example of a policy that blocks pull request builds from untrusted origins.

[source,rego]
----
package org

import future.keywords

policy_name["forked_pull_requests"]

# this rule is enabled only if the body evaluates to true (origin_repository_url and target_repository_url will be different in case of a forked pull request)
enable_rule["check_forked_builds"] {
	data.meta.vcs.origin_repository_url != data.meta.vcs.target_repository_url
}

# enable hard failure
hard_fail["check_forked_builds"]

check_forked_builds = reason {
	not from_trusted_origin(data.meta.vcs.origin_repository_url)
	reason := sprintf("pipeline triggered from untrusted origin: %s", [data.meta.vcs.origin_repository_url])
}

from_trusted_origin(origin) {
	some trusted_origin in {
		"https://github.com/trusted_org/",
		"https://bitbucket.org/trusted_org/",
	}

	startswith(origin, trusted_origin)
}
----

The following is an example of a policy that blocks SSH reruns on configs where a job uses sensitive contexts.

[source,rego]
----
package org

import future.keywords
import data.circleci.utils

policy_name["ssh_rerun"]

enable_hard["disallow_ssh_rerun"]

sensitive_contexts := { "secops", "deploy_keys", "access_tokens", "security" }

disallow_ssh_rerun = "Cannot perform ssh_rerun with sensitive contexts" {
    data.meta.ssh_rerun
    some _, job in input.workflows[_].jobs[_]
    count(utils.to_set(job.context) & sensitive_contexts) > 0
}
----

[#policies-and-parameterization]
== Policies and Config 2.1 Parameterization

Writing policies for CircleCI Config version 2.1 configuration can be challenging because Config 2.1 is designed to be parameterizable and reusable.
(Refer to the link for reusable configs.)

Before executing any pipelines, Config 2.1 is compiled into Config 2.0, which expands all parameters and reusable config blocks (executors and commands) into workflows and jobs.
To write highly effective policies, it is essential to reference the compiled version of the config as input._compiled_ in the policy.

Let's examine an example policy and config:

[source,rego]
----
import future.keywords

policy["name"] = "example_mistake"

enable_hard["enforce_not_large_resource"]

enforce_not_large_resource[reason] {
  some job_name, job in input.jobs
  job.resource_class == "large"
  reason = sprintf("job %s using banned large resource class", [job_name])
}
----

Config:

[source,yaml]
----
version: 2.1

executors:
  lg-executor:
    docker:
      - image: my-image
    resource_class: large

jobs:
  test:
    executor: lg-executor
----

In the above example, the policy would not trigger because it inspects the jobs and does not find a resource_class == "large".
However, once the config is compiled, the job test will have a resource_class of large.

Another way to bypass the policy is by using parameters:

[source,yaml]
----
version: 2.1

jobs:
  test:
    parameters:
      size:
        type: string
    resource_class: << parameters.size >>

workflows:
  main:
    jobs:
      - test:
          size: large
----

The same situation applies to the above config. The policy inspects the jobs and does not find a resource_class value of large, but instead finds << parameters.size >>, which is acceptable.
However, once the config is compiled, the job test will have a resource_class of large.

To resolve this issue, it is important to acknowledge that we want to apply the policy to all jobs, which is a Config 2.0 construct, and write the policy to target the compiled version accordingly.

If we modify the rule, we get the following:

[source,rego]
----
enforce_not_large_resource[reason] {
  some job_name, job in input._compiled_.jobs
  job.resource_class == "large"
  reason = sprintf("job %s using banned large resource class", [job_name])
}
----

Notice that we are now validating input._compiled_.jobs. Regardless of parameters or reusable blocks (executors), the policy will be applied to all compiled jobs and function as intended.

[##policing-2.1-constructs]
== Policing Config 2.1 Constructs

Often, we need to write policies against Config 2.1 level constructs such as orbs, executors, and commands. Writing policies against these constructs encounters the same parameterization challenges as described in the previous section.
However, we cannot rely on the compiled input because these constructs do not exist in Config version 2.0.

Let's consider another example to illustrate this.

Policy:

[source,rego]
----
import future.keywords

policy_name["example"]

enable_hard["ban_bad_orb_namespace"]

ban_bad_orb_namespace[reason] {
  some key, orb_ref in input.orbs
  startswith(orb_ref, "bad/")
  reason := sprintf("orb %s is defined with a banned namespace: bad", [key])
}
----

Config:

[source,yaml]
----
version: 2.1

parameters:
  evil_orb:
    type: string
    default: bad/orb

orbs:
  security: << pipeline.parameters.evil_orb >>
----

In the above example, the rule does not raise a violation because the string << pipeline.parameters.evil_orb >> does not have the `bad/` prefix that the policy aims to detect.
Furthermore, we cannot rely on `input._compiled_` because orbs are compiled away at that stage.

The best approach is to detect if an orb reference is a parameterized expression and raise a violation accordingly.

[source,rego]
----
import future.keywords
import data.circleci.config

policy_name["example"]

enable_hard["ban_bad_orb_namespace"]

ban_bad_orb_namespace = { reason |
  some key, orb_ref in input.orbs
  startswith(orb_ref, "bad/")
  reason := sprintf("orb %s is defined with a banned namespace: bad", [key])
} | { reason |
  some key, orb_ref in input.orbs
  config.is_parameterized_expression(orb_ref)
  reason := sprintf("orb %s is not allowed to contain a parameterized expression", ["key"])
}
----

[#use-sets-and-variables]
== Use sets and variables

It is best practice to avoid hard coding values in code, and the same goes for your config policies. Hard coding data, such as project IDs, makes it difficult to read code, and can be confusing when collaborating with wider team members ("what is `99ada477-7029-44bb-b675-5b2d6448d1ab`?"). Because using rego means your policies are defined in code, you can define sets and variables in rego files external to your individual policies, and reference these sets and variables across multiple policies. For an example of this in practice, see the xref:manage-contexts-with-config-policies#use-sets-and-variables[Manage contexts with config policies] page.

For further reading, see the link:https://circleci.com/blog/compliance-with-config-policies/[Config policies blog post].

[#testing-policies]
== Testing policies

It is important to be able to deploy new policies with confidence, knowing how they will be applied, and the decisions they will generate ahead of time. To enable this process, the `circleci policy test` command is available. The `test` subcommand is inspired by the golang and opa test commands. For more information on setting up testing, see the xref:test-config-policies#[Test config policies] guide.

[#dynamic-config]
== Config policies and dynamic configuration

You can write config policies to govern projects that use dynamic configuration too. Policies are evaluated against:

* _Setup_ configurations
* _Continuation_ configurations
* Standard configurations

If required for your project, you can encode rules to apply only to setup configs, or only to non-setup configs, as follows:

[source,rego]
----
enable_hard["setup_rule"] { input.setup } # only applied to configs with `setup: true`
----

[source,rego]
----
enable_hard["not_setup_rule"] { not input.setup } # only applied to configs that do not have `setup: true`
----

[source,rego]
----
enable_hard["some_rule"] # rule applied to all configs
----

For more information about dynamic configuration, see the xref:dynamic-config#[Dynamic configuration overview].

[#example-policy]
== Example policy

The following is an example of a complete policy with one rule, `use_official_docker_image`, which checks that
all Docker images in a config are prefixed by `circleci` or `cimg`. It uses some helper code to find all the `docker_images`
in the config. It then sets the enforcement status of `use_official_docker_image` to `hard_fail` and enables the rule.

This example also imports `future.keywords`, for more information see the link:https://www.openpolicyagent.org/docs/latest/policy-language/#future-keywords[OPA docs].

[source,rego]
----
package org

import future.keywords

policy_name["example"]

use_official_docker_image[image] = reason {
  some image in docker_images   # docker_images are parsed below
  not startswith(image, "circleci")
  not startswith(image, "cimg")
  reason := sprintf("%s is not an approved Docker image", [image])
}

# helper to parse docker images from the config
docker_images := {image | walk(input, [path, value])  # walk the entire config tree
                          path[_] == "docker"         # find any settings that match 'docker'
                          image := value[_].image}    # grab the images from that section

enable_hard["use_official_docker_image"]
----

[#next-steps]
== Next steps

* xref:create-and-manage-config-policies#[Create and manage config policies]
* xref:test-config-policies#[Test config policies]
* xref:use-the-cli-for-config-and-policy-development#[Use the CircleCI CLI for config and policy development]
* xref:config-policy-reference#[Config policy reference]
